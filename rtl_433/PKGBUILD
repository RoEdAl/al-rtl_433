# Maintainer: Dominik Heidler <dominik@heidler.eu>
# Modifications: Edmunt Pienkowsky <roed@onet.eu>

pkgname=rtl_433
pkgver=21.12
pkgrel=1
pkgdesc='Program to decode traffic from devices that are broadcasting on 433.9 MHz like temperature sensors'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
depends=('rtl-sdr')
makedepends=('git' 'gcc' 'cmake')
optdepends=()
url='https://github.com/merbanan/rtl_433'
source=(
    "${pkgname}-${pkgver}.tar.gz::http://github.com/merbanan/${pkgname}/archive/${pkgver}.tar.gz"
    '0001-LaCrosse-TX29-IT-Increase-pulse-width-to-58us.patch'
    '0002-Do-not-select-release-build-type-as-default.patch'
)
md5sums=('26178f9f5c194dd504797afecb228b05'
         '7ae6425325642dc2965bd145604a0671'
         '59a294c16e68262e8026b3b551c63fb1')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	git apply ${srcdir}/0001-LaCrosse-TX29-IT-Increase-pulse-width-to-58us.patch
	git apply ${srcdir}/0002-Do-not-select-release-build-type-as-default.patch
}

build() {
	mkdir -p ${srcdir}/build
	cd ${srcdir}/build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ../${pkgname}-${pkgver}
	cd ${srcdir}
	cmake --build build --clean-first
}

package() {
	cmake --build "${srcdir}/build" -- DESTDIR="${pkgdir}" install
	mv ${pkgdir}/usr/etc ${pkgdir}
}
